// ----- File Generated By ESE Plugin Generator ----- //

//bool DISCORD_RICH_PRESENCE_ENABLED;
bool DISCORD_RICH_PRESENCE_INIT;
bool DISCORD_RICH_PRESENCE_RUNNING;
ScriptCallQueue DISCORD_RICH_PRESENCE_CALLQUEUE;
ProcessHandle DISCORD_RICH_PRESENCE_HANDLE;
autoptr map<string, WBModuleDef> DISCORD_RICH_PRESENCE_OPEN_MODULES = new map<string, WBModuleDef>();

#ifndef ESE_INSTALLED
// for using this without ESE, create the workbench modules enum here
enum EWorkbenchModules
{
	ParticleEditor,
	AnimEditor,
	AudioEditor,
	BehaviorEditor,
	NavmeshGeneratorMain,
	ProcAnimEditor,
	ScriptEditor,
	ResourceManager,
	WorldEditor,
	LocalizationEditor
}
#endif

// This is a global function since the reference counting keeps the 'DiscordRP' instance alive through plugin reloads if a method is running in another thread

void DiscordRP_Run()
{
	string currentAddon;
	string currentModule;
	string currentFile;
	
	array<string> addons = {};
	GameProject.GetAvailableAddons(addons);
	currentAddon = GameProject.GetAddonTitle(addons[0]);
	
	Print("Running Discord Rich Presence...");
	
	while (DISCORD_RICH_PRESENCE_RUNNING)
	{
		if (DISCORD_RICH_PRESENCE_OPEN_MODULES.Get("ScriptEditor"))
		{
			if (currentModule != "ScriptEditor")
			{
				currentModule = "ScriptEditor";
			}
			ScriptEditor module = DISCORD_RICH_PRESENCE_OPEN_MODULES.Get("ScriptEditor");
			string newFile;
			module.GetCurrentFile(newFile);
			if (newFile != currentFile)
			{
				currentFile = newFile;
				// update state file
				FileHandle handle = FileIO.OpenFile(DiscordRP.GetStateFile(false), FileMode.WRITE);
				handle.FPrintln(FilePath.StripPath(currentFile));
				handle.FPrintln(currentAddon);
				handle.FPrintln("scripteditor");
				handle.CloseFile();
				Print("File Updated");
			}
		}
		else
		{
			string newModule = "ResourceManager";
			if (currentModule != newModule)
			{
				currentModule = newModule;
				currentFile = "No File Open...";
				
				FileHandle handle = FileIO.OpenFile(DiscordRP.GetStateFile(false), FileMode.WRITE);
				handle.FPrintln(currentFile);
				handle.FPrintln(currentAddon);
				handle.FPrintln("resourcemanager");
				handle.CloseFile();
				Print("File Updated");
			}
		}
		
		Sleep(5000);
	}
}

[WorkbenchPluginAttribute(name: "", description: "Discord Rich Presence", shortcut: "", icon: "", wbModules: {"ResourceManager"}, category: "", awesomeFontCode: 0)]
class DiscordRP: WorkbenchPlugin
{	
	protected static const string m_sRichPresencePath 		= "$EnforceScriptExtensions:scripts/WorkbenchGame/DiscordRP/";
	protected static const string m_sRichPresenceExecutable = "EnfusionWorkbenchRP.exe";
	protected static const string m_sRichPresenceEnabled	= "RichPresenceEnabled.c";

	static string GetStateFile(bool absolutePath=false)
	{
		if (!absolutePath)
			return m_sRichPresencePath + "RPState.txt";
		
		string absPath;
		Workbench.GetAbsolutePath(m_sRichPresencePath + "RPState.txt", absPath, true);
		return absPath;
	}
	
	static string GetExecutablePath(bool absolutePath=true)
	{
		if (!absolutePath)
			return m_sRichPresencePath + m_sRichPresenceExecutable;
		
		string absPath;
		Workbench.GetAbsolutePath(m_sRichPresencePath + m_sRichPresenceExecutable, absPath, true);
		return absPath;
	}
	
	static bool ReadEnabledState()
	{
		FileHandle handle = FileIO.OpenFile(m_sRichPresencePath + m_sRichPresenceEnabled, FileMode.READ);
		if (!handle)
		{
			PrintFormat("(ESE DiscordRP) %1%2 - File Missing!", m_sRichPresencePath, m_sRichPresenceEnabled);
		}
		string lineOut;
		handle.FGets(lineOut);
		handle.CloseFile();
		if (!lineOut)
			return false;
		return lineOut.ToInt();
	}
	
	static void WriteEnabledState(bool state)
	{
		FileHandle handle = FileIO.OpenFile(m_sRichPresencePath + m_sRichPresenceEnabled, FileMode.WRITE);
		if (!handle)
		{
			PrintFormat("(ESE DiscordRP) %1%2 - File Missing!", m_sRichPresencePath, m_sRichPresenceEnabled);
		}
		handle.FPrint("bool DISCORD_RICH_PRESENCE_ENABLED = " + state.ToString(false) + ";");
		handle.CloseFile();
	}

	override void RunCommandline() {}

	override void Configure() {}
	
	void DiscordRP()
	{
		//DISCORD_RICH_PRESENCE_ENABLED = ReadEnabledState();		
		if (!DISCORD_RICH_PRESENCE_ENABLED)
		{
			return;
		}
		
		DISCORD_RICH_PRESENCE_HANDLE = Workbench.RunProcess(GetExecutablePath(true));
		DISCORD_RICH_PRESENCE_RUNNING = true;
		thread DiscordRP_Run();
	}
	
	void ~DiscordRP()
	{
		Workbench.KillProcess(DISCORD_RICH_PRESENCE_HANDLE);
		DISCORD_RICH_PRESENCE_RUNNING = false;
	}
}

// Empty workbench plugins unique to each editor, used to tell what editors are open

[WorkbenchPluginAttribute(name: "", wbModules: {"ParticleEditor"})]
class ParticleEditorActive: 			WorkbenchPlugin {
	void ParticleEditorActive()			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("ParticleEditor", Workbench.GetModule(ParticleEditor)); } 
	void ~ParticleEditorActive()		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("ParticleEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"AnimEditor"})]
class AnimEditorActive: 				WorkbenchPlugin {
	void AnimEditorActive()  			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("AnimEditor", Workbench.GetModule(AnimEditor)); }
	void ~AnimEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("AnimEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"AudioEditor"})]
class AudioEditorActive: 				WorkbenchPlugin {
	void AudioEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("AudioEditor", Workbench.GetModule(AudioEditor)); }
	void ~AudioEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("AudioEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"BehaviorEditor"})]
class BehaviorEditorActive: 			WorkbenchPlugin {
	void BehaviorEditorActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("BehaviorEditor", Workbench.GetModule(BehaviorEditor)); }
	void ~BehaviorEditorActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("BehaviorEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"NavmeshGeneratorMain"})]
class NavmeshGeneratorMainActive: 		WorkbenchPlugin {
	void NavmeshGeneratorMainActive() 	{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("NavmeshGeneratorMain", Workbench.GetModule(NavmeshGeneratorMain)); }
	void ~NavmeshGeneratorMainActive() 	{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("NavmeshGeneratorMain"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"ProcAnimEditor"})]
class ProcAnimEditorActive: 			WorkbenchPlugin {
	void ProcAnimEditorActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("ProcAnimEditor", Workbench.GetModule(ProcAnimEditor)); }
	void ~ProcAnimEditorActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("ProcAnimEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"ScriptEditor"})]
class ScriptEditorActive: 				WorkbenchPlugin {
	void ScriptEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("ScriptEditor", Workbench.GetModule(ScriptEditor)); }
	void ~ScriptEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("ScriptEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"ResourceManager"})]
class ResourceManagerActive: 			WorkbenchPlugin {
	void ResourceManagerActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("ResourceManager", Workbench.GetModule(ResourceManager)); }
	void ~ResourceManagerActive() 		{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("ResourceManager"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"WorldEditor"})]
class WorldEditorActive: 				WorkbenchPlugin {
	void WorldEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("WorldEditor", Workbench.GetModule(WorldEditor)); }
	void ~WorldEditorActive() 			{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("WorldEditor"); }
}

[WorkbenchPluginAttribute(name: "", wbModules: {"LocalizationEditor"})]
class LocalizationEditorActive: 		WorkbenchPlugin {
	void LocalizationEditorActive() 	{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Insert("LocalizationEditor", Workbench.GetModule(LocalizationEditor)); }
	void ~LocalizationEditorActive() 	{ DISCORD_RICH_PRESENCE_OPEN_MODULES.Remove("LocalizationEditor"); }
}
